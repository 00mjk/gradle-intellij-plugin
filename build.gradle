plugins {
    id 'com.gradle.plugin-publish' version '0.9.4'
    id 'maven'
    id 'java-gradle-plugin'
}

apply plugin: 'groovy'

compileGroovy {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    maven { url mavenCentralUrl }
}

dependencies {
    compile localGroovy()
    compile 'org.jetbrains:annotations:13.0'
    compile 'org.jetbrains.intellij:plugin-repository-rest-client:0.3.16'
    compile 'org.jetbrains.intellij.plugins:intellij-plugin-structure:1.23'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude module: 'groovy-all'
    }
    testCompile 'junit:junit:4.12'
}

version = '0.1.9'
group = 'org.jetbrains'

pluginBundle {
    website = 'https://jetbrains.com/'
    vcsUrl = 'https://github.com/JetBrains/gradle-intellij-plugin'
    description = 'Plugin for building plugins for IntelliJ IDEs'
    tags = ['intellij', 'jetbrains', 'idea']

    //noinspection GroovyAssignabilityCheck
    plugins {
        intellijPlugin {
            id = 'org.jetbrains.intellij'
            displayName = 'Gradle IntelliJ Plugin'
        }
    }
}

test {
    doFirst {
        new File("$buildDir/localRepo").mkdir()
        new File("$buildDir/testGradleHome").mkdir()
    }
    systemProperties['local.repo'] = "$buildDir/localRepo"
    systemProperties['test.gradle.home'] = "$buildDir/testGradleHome"
    dependsOn install
}

install {
    repositories {
        mavenDeployer {
            repository(url: new File("$buildDir/localRepo").toURI())
        }
    }
}